edit-command-line () {
  emulate -L zsh
  local left right prebuffer buffer=$BUFFER lbuffer=$LBUFFER
  local TMPSUFFIX=.zsh
  if (( REGION_ACTIVE ))
  then
    if (( CURSOR < MARK ))
    then
      left=$CURSOR right=$MARK
      lbuffer=
    else
      left=$MARK right=$CURSOR
      lbuffer[right-left,-1]=
    fi
    (( left++ ))
    buffer=$BUFFER[left,right]
  elif (( ! ZLE_RECURSIVE ))
  then
    prebuffer=$PREBUFFER
  fi
  () {
    exec < /dev/tty
    setopt localoptions nomultibyte noksharrays
    (( $+zle_bracketed_paste )) && print -r -n - $zle_bracketed_paste[2]
    local -a editor
    zstyle -a :zle:$WIDGET editor editor
    if (( ! $#editor ))
    then
      editor=("${(@Q)${(z)${VISUAL:-${EDITOR:-vi}}}}")
    fi
    case $editor in
      (*vim*) integer byteoffset=$(( $#prebuffer + $#lbuffer + 1 ))
        if [ -n "$TMUX" ]; then
          tmux popup -E -w 75% -e PATH="$HOME/.local/bin:$PATH" "\"${(@)editor}\" -c \"normal! ${byteoffset}go\" -- $1"
        else
          "${(@)editor}" -c "normal! ${byteoffset}go" -- $1
        fi
        ;;
      (*emacs*) local lines=("${(@f):-"$prebuffer$lbuffer"}")
        "${(@)editor}" +${#lines}:$((${#lines[-1]} + 1)) $1 ;;
      (*) "${(@)editor}" $1 ;;
    esac
    (( $+zle_bracketed_paste )) && print -r -n - $zle_bracketed_paste[1]
    if (( REGION_ACTIVE ))
    then
      local prelen=$#BUFFER
      BUFFER[left,right]="$(<$1)"
      if (( MARK > CURSOR ))
      then
        (( MARK += $#BUFFER - prelen ))
      else
        (( CURSOR += $#BUFFER - prelen ))
      fi
    elif [[ $CONTEXT != cont ]] || (( ZLE_RECURSIVE ))
    then
      BUFFER="$(<$1)"
    else
      print -Rz - "$(<$1)"
      zle send-break
    fi
  } =(<<<"$prebuffer$buffer")
}
